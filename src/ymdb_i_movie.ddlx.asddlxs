@Metadata.layer: #CORE
annotate entity YMDB_I_MOVIE with
{
  @UI.facet: [{
        id: 'idGeneralInfo',
        type: #COLLECTION,
        label: 'Movie',
        position: 10 
    }, { 
        id: 'idMovieData',
        parentId: 'idGeneralInfo',
        type: #IDENTIFICATION_REFERENCE,
        label: 'Movie Data',
        position: 20
    }, { 
        id: 'idStatisticData',
        parentId: 'idGeneralInfo',
        type: #FIELDGROUP_REFERENCE,
        label: 'Statistic Data',
        targetQualifier: 'tqStatistic',
        position: 30
    }, {
        id: 'idMyData',
        purpose: #STANDARD,
        label: 'My Data',
        position: 30,
        parentId: 'idGeneralInfo',
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'tqMyData'
    }, {
        id: 'Role',
        purpose: #STANDARD,
        type: #LINEITEM_REFERENCE,
        label: 'Roles',
        position: 40,
        targetElement: '_Role'
    }, {
        id : 'idChart',
        type: #CHART_REFERENCE,
        label : 'Votes',
        targetElement: '_Chart',
        position: 35
    }, { 
        id: 'idlinks',
        purpose: #STANDARD,
        label: 'Links',
        position: 50,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'tqLinks'
    }]
  
  MovieId;

  @UI.lineItem: [{ position:20, type: #WITH_URL, url: 'LinkToImdb' }]
  @UI.identification: [ {position: 30} ]
  @UI.selectionField: [{ position: 0 }]
  @EndUserText.label: 'Movie Title'
  Title;

  @UI.lineItem: [{ position: 30 }] 
  @UI.identification: [{ position: 40}]
  @UI.selectionField: [{ position: 10 }]
  @EndUserText.label: 'Category'
  Category;
  
  @UI.lineItem: [{ position:40 }]
  @UI.identification: [ {position: 50} ]
  @UI.selectionField: [{ position: 20 }]
  @EndUserText.label: 'Release Date'
  ReleaseDate;
  
  @UI.lineItem: [{ position: 50 }]
  @UI.identification: [{ position: 60 }]
  @UI.selectionField: [{ position: 30 }]
  PEGI;
  
  @UI.lineItem: [{ position: 60 }]
  @UI.identification: [{ position: 70 }]
  @EndUserText.label: 'Runtime in minutes'
  Runtime;

  @UI.hidden: true
  LastChangedAt;
  
  @UI.fieldGroup: [{ qualifier: 'tqLinks', position: 10, type: #WITH_URL, url: 'LinkToImdb' }]
  LinkToImdb;
  
  @UI.lineItem: [{ importance: #HIGH, position: 05}]
  @UI.fieldGroup: [{ qualifier: 'tqLinks', position: 20, type: #WITH_URL, url: 'MoviePicURL' }]
  @EndUserText.label: 'Picture'
  MoviePicURL;

}